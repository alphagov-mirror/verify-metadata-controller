allprojects {
    repositories {
        if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
            logger.warn('Production builds MUST NOT be built with public binaries.\nUse artifactory/allowed-repos for production builds.\n\n')
            maven { url 'https://dl.bintray.com/alphagov/maven-test' }
            maven { url 'https://dl.bintray.com/alphagov/maven' }
            maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases' }
            jcenter()
        } else {
            maven { url 'https://gds.jfrog.io/artifactory/allowed-repos' }
        }
        // cloudhsm libraries downloaded from AWS
        flatDir {
            dirs '/opt/cloudhsm/java'
        }
    }
}

subprojects {

    apply plugin: 'java'

    // Dependency report across all modules
    task allDeps(type: DependencyReportTask) {}

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    group = "uk.gov.ida.${rootProject.name}"

    configurations {
        common
        cloudhsmtool
        mdgen
        testing
    }

    dependencies {

        configurations.all {
                exclude group: 'io.dropwizard'
                exclude group: 'org.eclipse.jetty'
                exclude group: 'org.springframework'
                exclude group: 'org.opensaml', module: 'xmltooling'
        }

        common(
                'org.apache.logging.log4j:log4j-api:2.9.1',
                'org.apache.logging.log4j:log4j-core:2.9.1',
                'org.slf4j:slf4j-simple:1.7.25',
                'info.picocli:picocli:4.2.0',
                'org.bouncycastle:bcprov-jdk15on:1.64',
                'org.opensaml:opensaml-xmlsec-api:3.4.5',
        )

        cloudhsmtool(
                'org.bouncycastle:bcpkix-jdk15on:1.64',
        )

        mdgen(
                'org.opensaml:opensaml-core:3.4.5',
                'org.opensaml:opensaml-saml-api:3.4.5',
                'org.opensaml:opensaml-saml-impl:3.4.5',
                'org.opensaml:opensaml-xmlsec-impl:3.4.5',
                'se.litsec.opensaml:opensaml3-ext:1.2.2',
                'com.github.spullara.mustache.java:compiler:0.9.6',
                'org.yaml:snakeyaml:1.26'
        )

        testing(
                'junit:junit:4.13',
                'org.junit.jupiter:junit-jupiter-api:5.5.2',
                'org.assertj:assertj-core:3.15.0',
                'org.mockito:mockito-core:3.3.3',
                'uk.gov.ida:ida-dev-pki:1.1.0-37',
                'uk.gov.ida:security-utils:2.0.0-374',
                'uk.gov.ida:saml-test:3.4.3-232'
        )
    }

    dependencies {

        if (project.hasProperty('cloudhsm')) {
            implementation name: 'cloudhsm-3.2.1'
        } else {
            implementation files('../dev/cloudhsm-3.2.1.jar')
        }
    }
}

defaultTasks 'clean', 'test', 'installDist'